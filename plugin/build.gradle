plugins {
    id 'java-gradle-plugin'
    id 'org.jetbrains.kotlin.jvm' version '1.4.31'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.14.0'
}

repositories {
    mavenCentral()
}

group = 'com.k4zy'
version = '0.1'

gradlePlugin {
    // Define the plugin
    plugins {
        timeTraceReporter {
            id = 'com.k4zy.time-trace'
            implementationClass = 'com.k4zy.timetrace.TimeTraceReporterPlugin'
            displayName = "Time trace plugin"
            description = "Measure the execution time of each task."
        }
    }
}

pluginBundle {
    website = 'https://github.com/k4zy/TimeTraceReporter'
    vcsUrl = 'https://github.com/k4zy/TimeTraceReporter.git'
    tags = ['performance', 'build', 'metrics']
}

sourceSets {
    functionalTest {
    }
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)

tasks.register('functionalTest', Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
}

tasks.named('check') {
    dependsOn(tasks.functionalTest)
}

dependencies {
    implementation platform('org.jetbrains.kotlin:kotlin-bom')
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'com.github.doyaaaaaken:kotlin-csv-jvm:0.15.2'
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
    testImplementation gradleTestKit()
    functionalTestImplementation gradleTestKit()
    functionalTestImplementation  'org.jetbrains.kotlin:kotlin-test'
    functionalTestImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
}
